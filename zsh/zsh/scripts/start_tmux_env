#!/usr/bin/zsh

COMMAND=${0:t}
ENV_DIR_PATH=${0:A:h}/tmux_env

init_variables() {
  AVAILABLE_TARGETS=( $(
    ls "${ENV_DIR_PATH}"
  ) )

  TARGET=
}


usage() {
  cat << EOF
usage: ${COMMAND} [<options>] <target>

Start target tmux environment

Options:
  -h,  --help     show this message only

Available targets: ${AVAILABLE_TARGETS}
EOF
}


error() {
  if [[ -n "$1" ]]; then
    echo "error: $1" >&2
  fi
  usage
  exit 1
}


start_target_tmux_env() {
  local host="$(
    cat "${ENV_DIR_PATH}/${TARGET}/host"
  )"
  local scripts_path=( $(
    ls "${ENV_DIR_PATH}/${TARGET}"/*.sh
  ) )

  fortune -acws -n 300

  if [[ "${host}" == 'localhost' ]]; then
    for script_path in ${scripts_path}; do
      sh "${script_path}"
    done

    tmux attach-session

  else

    for script_path in ${scripts_path}; do
      ssh "${host}" 'sh -s' < "${script_path}"
    done

    ssh -t "${host}" tmux attach-session
  fi
}


parse_options() {
  while [[ "$#" -gt 0 ]]; do
    case "$1" in
      -h|--help)
        usage
        exit 0
        ;;
      *)
        break 2
        ;;
    esac
  done

  [[ $# -eq 0 ]] && error 'missing target'

  TARGET="$1"
}


validate_options() {
  # validate target
  if ! (( ${AVAILABLE_TARGETS[(Ie)${TARGET}]} )); then
    error "invalid target ${TARGET}"
  fi
}


init_variables
parse_options "$@"
validate_options

start_target_tmux_env
