#!/usr/bin/zsh

COMMAND=${0:t}
ENV_DIR_PATH=${0:A:h}/tmux_env

init_variables() {
  AVAILABLE_TARGETS=( $(
    ls "${ENV_DIR_PATH}"
  ) )

  TARGET=
  SESSION=
}


usage() {
  cat << EOF
usage: ${COMMAND} [<options>] <target> [<session>]

Start target tmux environment sessions

Options:
  -h,  --help     show this message only

Available targets: ${AVAILABLE_TARGETS}
If session is not defined, all of the target's ones are started
EOF
}


error() {
  if [[ -n "$1" ]]; then
    echo "error: $1" >&2
  fi
  usage
  exit 1
}


get_session_pattern() {
  local script_path="$1"

  local session_name="${script_path:t:r}"

  # there is no way currently to check session name case insensitively
  # (no flag used in fnmatch internally in tmux)
  # so we create a pattern manually for it
  local session_pattern='*'
  for i in $( seq 0 $(( ${#session_name} - 1 )) ); do
    local char="${session_name:$i:1}"
    local char_low=$( echo "${char}" | tr 'A-Z' 'a-z' )
    local char_up=$( echo "${char}" | tr 'a-z' 'A-Z' )
    session_pattern+="[${char_low}${char_up}]"
  done
  session_pattern+='*'

  echo "${session_pattern}"
}


start_target_tmux_env() {
  local host="$(
    cat "${ENV_DIR_PATH}/${TARGET}/host"
  )"

  local scripts_path
  if [[ -n "${SESSION}" ]]; then
    scripts_path=(
      "${ENV_DIR_PATH}/${TARGET}/${SESSION}.sh"
    )
  else
    scripts_path=( $(
      ls "${ENV_DIR_PATH}/${TARGET}"/*.sh
    ) )
  fi

  fortune -acws -n 300

  if [[ "${host}" == 'localhost' ]]; then
    for script_path in ${scripts_path}; do
      local session_pattern=$( get_session_pattern "${script_path}" )
      if ! tmux has-session -t "${session_pattern}" &> /dev/null; then
        bash "${script_path}"
      fi
    done

    tmux attach-session

  else

    for script_path in ${scripts_path}; do
      local session_pattern=$( get_session_pattern "${script_path}" )
      if ! ssh "${host}" tmux has-session -t "${session_pattern}" &> /dev/null; then
        ssh "${host}" 'bash -s' < "${script_path}"
      fi
    done

    ssh -t "${host}" tmux attach-session
  fi
}


parse_options() {
  while [[ "$#" -gt 0 ]]; do
    case "$1" in
      -h|--help)
        usage
        exit 0
        ;;
      *)
        break 2
        ;;
    esac
  done

  [[ $# -eq 0 ]] && error 'missing target'

  TARGET="$1"
  SESSION="$2"
}


validate_options() {
  # validate target
  if ! (( ${AVAILABLE_TARGETS[(Ie)${TARGET}]} )); then
    error "invalid target ${TARGET}"
  fi

  # validate target's session if present
  if [[ -n "${SESSION}" ]] \
       && [[ ! -f "${ENV_DIR_PATH}/${TARGET}/${SESSION}.sh" ]]; then
    error "invalid target's session ${SESSION}"
  fi
}


init_variables
parse_options "$@"
validate_options

start_target_tmux_env
