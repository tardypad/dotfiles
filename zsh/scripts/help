#!/usr/bin/env zsh

COMMAND=${0:t}

init_variables() {
  TOPIC=
}


usage() {
  cat << EOF
usage: ${COMMAND} [<options>] [<topic>]

Get help about a topic

Options:
  -h,  --help           show this message only
EOF
}


search_help() {
  if [[ -z "${TOPIC}" ]]; then
      echo -n 'about? '
      read TOPIC
  fi

  if man "${TOPIC}" &> /dev/null; then
    display_man
  elif "${TOPIC}" --help &> /dev/null; then
    display_help
  else
    display_no_help
  fi
}


display_man() {
  man "${TOPIC}"
  return 0
}


display_help() {
  "${TOPIC}" --help | less
  return 0
}


display_no_help() {
  echo "no help for '${TOPIC}' :("

  PS3='What to do now? '

  choices=(
    'another topic'
    "search help online for '${TOPIC}'"
    'rage quit'
  )

  select choice in ${choices}; do
    case $choice in
        'another topic')
          TOPIC=
          return 1
          ;;
        "search help online for '${TOPIC}'")
          xdg-open "http://www.google.com/search?q=man+${TOPIC}"
          return 0
          ;;
        'rage quit')
          return 0
          ;;
    esac
  done
}


parse_options() {
  while [[ "$#" -gt 0 ]]; do
    case "$1" in
      -h|--help)
        usage
        exit 0
        ;;
      *)
        break 2
        ;;
    esac
  done

  TOPIC="$1"
}


init_variables
parse_options "$@"


old_columns=$COLUMNS
export COLUMNS=1

until search_help; do : ; done

COLUMNS=$old_columns
