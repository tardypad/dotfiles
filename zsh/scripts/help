#!/usr/bin/env zsh

COMMAND=${0:t}

# use run-help utility
unalias run-help
autoload -Uz run-help

init_variables() {
  TOPIC=
}


usage() {
  cat << EOF
usage: ${COMMAND} [<options>] [<topic>]

Get help about a topic

Options:
  -h,  --help           show this message only
EOF
}


search_help() {
  if [[ -z "${TOPIC}" ]]; then
      echo -n 'about? '
      read TOPIC
  fi

  # capture run-help error messages
  local help_result=$( run-help "${TOPIC}" 2>&1 1> /dev/null )

  # If there is no possible help for the topic
  # run-help will output 'No manual entry for [topic]'
  # from a last fail "man" command
  # There doesn't seem to be another way to check success or failure
  # (return code is always 0)
  if [[ ! "${help_result}" =~ '.*No manual entry for.*' ]]; then
    display_run_help
  elif "${TOPIC}" --help &> /dev/null; then
    display_help_argument
  else
    display_no_help
  fi
}


display_run_help() {
  run-help "${TOPIC}"
  return 0
}


display_help_argument() {
  "${TOPIC}" --help | less
  return 0
}


display_no_help() {
  echo "no help for '${TOPIC}' :("

  # display the options in one column
  old_columns=$COLUMNS
  export COLUMNS=1

  PS3='What to do now? '

  choices=(
    'another topic'
    "search help online for '${TOPIC}'"
    'rage quit'
  )

  select choice in ${choices}; do
    case $choice in
        'another topic')
          TOPIC=
          return 1
          ;;
        "search help online for '${TOPIC}'")
          xdg-open "http://www.google.com/search?q=man+${TOPIC}"
          return 0
          ;;
        'rage quit')
          return 0
          ;;
    esac
  done

  COLUMNS=$old_columns
}


parse_options() {
  while [[ "$#" -gt 0 ]]; do
    case "$1" in
      -h|--help)
        usage
        exit 0
        ;;
      *)
        break 2
        ;;
    esac
  done

  TOPIC="$1"
}


init_variables
parse_options "$@"


until search_help; do : ; done
