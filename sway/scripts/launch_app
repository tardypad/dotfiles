#! /usr/bin/env zsh

APP_DESKTOP_FILES_PATH="${XDG_DATA_HOME:-$HOME/.local/share}/applications"

typeset -A AVAILABLE_APPS

for APP_DESKTOP_FILE in "${APP_DESKTOP_FILES_PATH}"/*.desktop; do
  APP_NAMES=$(
    grep --extended-regexp '^Name=|^GenericName=' "${APP_DESKTOP_FILE}" \
      | sort \
      | cut --delimiter = --fields 2 \
      | paste --delimiters @ - -
  )
  FILLER_LENGTH=$(( COLUMNS - ${#APP_NAMES} - 6 ))
  FILLER="${(l.$FILLER_LENGTH.. .)}"
  STYLED_APP_NAMES=$(
    echo "${APP_NAMES}" | sed "s/@/${FILLER}/"
  )
  AVAILABLE_APPS[${STYLED_APP_NAMES}]="${APP_DESKTOP_FILE}"
done

APPS=$(
  for STYLED_APP_NAMES in ${(k)AVAILABLE_APPS}; do
    echo "${STYLED_APP_NAMES}"
  done \
    | sort \
    | fzf --reverse \
          --multi \
          --margin 1,2,0,2 \
          --prompt 'run app ' \
          --color info:8
)

if [[ -n "${APPS}" ]]; then
  IFS=$'\n'
  for APP in $( echo ${APPS} ); do
    # not sure why running dex directly without using swaymsg
    # doesn't work when ran from the launcher
    swaymsg -t command \
      exec "dex ${AVAILABLE_APPS[$APP]}"
  done
fi
