#! /usr/bin/env zsh

typeset -A LIST

LIST=(
  'Cool'                 '(⌐■_■)'
  'Copyright'            '©'
  'Degree'               '°'
  'Euro'                 '€'
  'Flipping table'       '(╯°□°)╯︵┻━┻'
  'Flipping text'        '(╯°□°)╯︵ʇxǝʇ'
  'Happy Gary'           'ᕕ(ᐛ)ᕗ'
  'Look of Disapproval'  'ಠ_ಠ'
  'Registered'           '®'
  'Shrug'                '¯\_(ツ)_/¯'
  'Trademark'            '™'
)

display_item() {
  local name="$1"

  # there is a length calculation issue for character such as ツ
  # which are counted as 1 but are displayed on 2 cells
  local item=$LIST[${NAME}]
  local filler_length=$((
    COLUMNS - ${#name} - ${#item} - 8
  ))
  local filler="${(l.$filler_length.. .)}"

  echo "${name}${filler}${item}"
}

upside_down() {
  local text="$1"
  local result_text

  # can't use tr on non ascii characters...
  for (( i=0; i<${#text}; i++ )); do
    case "${text:$i:1}" in
      'a') result_text+='ɐ' ;;
      'b') result_text+='q' ;;
      'c') result_text+='ɔ' ;;
      'd') result_text+='p' ;;
      'e') result_text+='ǝ' ;;
      'f') result_text+='ɟ' ;;
      'g') result_text+='ɓ' ;;
      'h') result_text+='ɥ' ;;
      'i') result_text+='ᴉ' ;;
      'j') result_text+='ɾ' ;;
      'k') result_text+='ʞ' ;;
      'l') result_text+='l' ;;
      'm') result_text+='ɯ' ;;
      'n') result_text+='u' ;;
      'o') result_text+='o' ;;
      'p') result_text+='d' ;;
      'q') result_text+='b' ;;
      'r') result_text+='ɹ' ;;
      's') result_text+='s' ;;
      't') result_text+='ʇ' ;;
      'u') result_text+='n' ;;
      'v') result_text+='ʌ' ;;
      'w') result_text+='ʍ' ;;
      'x') result_text+='x' ;;
      'y') result_text+='ʎ' ;;
      'z') result_text+='z' ;;
      ' ') result_text+=' ' ;;
    esac
  done

  echo "$result_text" | rev
}


TYPE=$(
  for NAME in "${(@k)LIST}"; do
    display_item "${NAME}"
  done \
    | sort \
    | fzf --reverse \
          --no-multi \
          --margin 1,2,0,2 \
          --prompt 'copy ' \
          --color info:8
)

[[ -n "${TYPE}" ]] || exit


# Special case
if [[ "${TYPE}" =~ 'Flipping text*' ]]; then
  echo 'What to flip? (╯°□°)╯︵'
  read FLIP_TEXT
  FLIP_TEXT=$( echo "${FLIP_TEXT}" | tr --delete '\n' )

  [[ -n "${FLIP_TEXT}" ]] || exit

  swaymsg clipboard "(╯°□°)╯︵$( upside_down ${FLIP_TEXT} )"
  exit
fi

# if there ever are 2 items where one full name is the beginning of another
# then the loop search will need to start with items of longer length
# to match the proper item
for NAME in "${(@k)LIST}"; do
  if [[ "${TYPE}" =~ "${NAME}*" ]]; then
    swaymsg clipboard  "$LIST[${NAME}]"
    exit
  fi
done
