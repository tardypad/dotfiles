#! /usr/bin/env zsh

APP_DESKTOP_FILES_PATH="${XDG_DATA_HOME:-$HOME/.local/share}/applications"
CACHE_APPS_LIST_PATH="${XDG_CACHE_HOME:-$HOME/.cache}/sway/launch_app"

typeset -A AVAILABLE_APPS

build_available_apps() {
  for app_desktop_file in "${APP_DESKTOP_FILES_PATH}"/*.desktop; do
    local app_name=$(
      grep '^Name=' "${app_desktop_file}" \
        | cut --delimiter = --fields 2
    )
    local app_generic_name=$(
      grep '^GenericName=' "${app_desktop_file}" \
        | cut --delimiter = --fields 2
    )
    local app_icon=$(
      grep '^Icon=' "${app_desktop_file}" \
        | cut --delimiter = --fields 2
    )

    local filler_length=$((
      50 - ${#app_name} - ${#app_generic_name} - ${#app_icon}
    ))
    local filler="${(l.$filler_length.. .)}"

    local styled_app_names=$(
      echo "${app_icon}  ${app_generic_name}${filler}${app_name}"
    )
    AVAILABLE_APPS[${styled_app_names}]="${app_desktop_file}"
  done
}

check_build_cache() {
  if [[ -e "${CACHE_APPS_LIST_PATH}" ]]; then
    local cache_last_modified=$(
      stat --format '%Y' "${CACHE_APPS_LIST_PATH}"
    )
  else
    local cache_last_modified=0
  fi

  local files_last_modified=$(
    find "${APP_DESKTOP_FILES_PATH}" -type f -exec stat --format '%Y' {} \; \
      | sort \
      | tail --lines 1
  )

  if [[ "${cache_last_modified}" -lt "${files_last_modified}" ]]; then
    mkdir --parents "${XDG_CACHE_HOME:-$HOME/.cache}/sway/"

    build_available_apps

    for styled_app_names in ${(k)AVAILABLE_APPS}; do
      echo "${styled_app_names}"
    done > "${CACHE_APPS_LIST_PATH}"
  fi
}

check_build_cache

SELECTED_APPS=$(
  cat "${CACHE_APPS_LIST_PATH}" \
    | sort \
    | fzf --reverse \
          --multi \
          --margin 1,2,0,2 \
          --prompt 'run apps ' \
          --color info:{color/background/number}
)

if [[ -n "${SELECTED_APPS}" ]]; then
  [[ -n "${AVAILABLE_APPS}" ]] || build_available_apps

  IFS=$'\n'
  for SELECTED_APP in $( echo ${SELECTED_APPS} ); do
    # not sure why running exec_desktop_entry directly without using swaymsg
    # doesn't work when ran from the launcher
    swaymsg exec \
      "exec_desktop_entry ${AVAILABLE_APPS[$SELECTED_APP]}"
  done
fi
