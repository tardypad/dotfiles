#!/usr/bin/env zsh

COMMAND=${0:t}

init_variables() {
  ENV_DIR_PATH="${XDG_CONFIG_HOME:-$HOME/.config}/tmux/env/"

  AVAILABLE_TARGETS=( $(
    ls "${ENV_DIR_PATH}"
  ) )
  BACKGROUND=false

  TARGET=
  SESSION=
}


usage() {
  cat <<- EOF
	usage: ${COMMAND} [<options>] <target> [<session>]

	Start target tmux environment sessions

	Options:
	  -b,  --background     launch in background, no attach
	  -h,  --help           show this message only

	Available targets: ${AVAILABLE_TARGETS}
	If no session is defined, a new empty one is created
	EOF
}


error() {
  [[ -z "$1" ]] || echo "${COMMAND}: $1"
  echo "Try '${COMMAND} --help' for more information."
  exit 1
} >&2


is_session_present() {
  local session_names="$1"
  local session="$2"

  for session_name in ${(f)session_names}; do
    if [[ "${session}" = "${session_name}" ]]; then
      return 0
    fi
  done

  return 1
}


start_target_tmux_env() {
  local session_names=$(
    tmux -L "${TARGET}" list-sessions -F '#{session_name}' 2> /dev/null
  )

  if [[ -n "${SESSION}" ]]; then
    if ! is_session_present "${session_names}" "${SESSION}"; then
      local script_path="${ENV_DIR_PATH}/${TARGET}/${SESSION}.sh"
      TMUX_ENV=yes zsh "${script_path}" "${TARGET}" "${SESSION}"
    fi
  else
    SESSION='new'
    if ! is_session_present "${session_names}" "${SESSION}"; then
      TMUX_ENV=yes tmux -L "${TARGET}" new-session -d -s "${SESSION}"
    fi
  fi

  if ! $BACKGROUND; then
    tmux -L "${TARGET}" attach-session -t "${SESSION}" &> /dev/null
  else
    tmux -L "${TARGET}" switch-client -t "${SESSION}" &> /dev/null
  fi
}


parse_options() {
  while [[ "$#" -gt 0 ]]; do
    case "$1" in
      -b|--background)
        BACKGROUND=true
        shift
        ;;
      -h|--help)
        usage
        exit 0
        ;;
      *)
        break 2
        ;;
    esac
  done

  if [[ $# -eq 0 ]]; then
    error 'missing target'
  fi

  TARGET="$1"
  SESSION="$2"
}


validate_options() {
  if [[ -n "${TMUX}" ]]; then
    error "nested tmux env is not allowed"
  fi

  if ! (( ${AVAILABLE_TARGETS[(Ie)${TARGET}]} )); then
    error "invalid target ${TARGET}"
  fi

  if [[ -n "${SESSION}" \
     && ! -f "${ENV_DIR_PATH}/${TARGET}/${SESSION}.sh" ]]; then
    error "invalid target's session ${SESSION}"
  fi
}


init_variables
parse_options "$@"
validate_options

start_target_tmux_env
