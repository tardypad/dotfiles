#compdef task

local logging_dir="${XDG_DATA_HOME:-$HOME/.local/share}/tasks"

_actions() {
  local actions=(
    ids
    log
    overview
    review
    show
    start
    stop
  )

  compadd "$@" $actions
}

_ids() {
  local ids=( "${(@f)$( task ids )}" )
  compadd "$@" $ids
}

_days() {
  _path_files -W "${logging_dir}"
}

_operands() {
  if [ $words[CURRENT-1] = 'start' ] \
    || [ $words[CURRENT-1] = 'log' ]; then
    _ids
  elif [ $words[CURRENT-1] = 'review' ] \
    || [ $words[CURRENT-1] = 'overview' ]; then
    _days
  fi
}

_arguments \
  '-g[display logs grouped by identifier]' \
  '-n[show identifiers with their name]' \
  '-q[do not log the task at its end]' \
  '-t[display logs with values separated by tab]' \
  '1:action:_actions' \
  '2:operand:_operands'
